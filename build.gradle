
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0'
  }
}

apply from: "${rootDir}/gradle/publishing.gradle"

task cleanDockerImages(type: Exec, description: 'Clean up dangling docker images.', group: 'docker') {
  executable "sh"
  args "-c", "docker rmi \$(docker images -aq --filter dangling=true) || true"

  // to clean up all our custom images:
  //docker rmi \$(docker images -aq --filter "label=ncei.metadata-catalog-vendor")
}

clean.dependsOn cleanDockerImages

task debugDockerLogs(type: Exec, description: 'Print the docker logs before removing the containers.', group: 'docker') {
  executable "bash"
  args "-c", "docker ps -aq | xargs -n 1 docker logs || true"
}

task promote(type: Exec, description: 'Promotes artifactory snapshot', group: 'publish') {
  executable "bash"
  args "-c", "curl -X POST -u \"$bintrayUser:$bintrayKey\" -H \"Content-Length: 0\"\
    \"http://oss.jfrog.org/api/plugins/build/promote/snapshotsToBintray/${rootProject.name}/$travisBuildNumber?params=releaseVersion=$baseVersion\""
}

publishing {
  publications {
    main(MavenPublication) {
      artifact "docker-compose.yml"

      groupId 'edu.colorado.cires.metadata-catalog'
      artifactId "${rootProject.name}"
      version "${project.version}"
    }
  }
}
