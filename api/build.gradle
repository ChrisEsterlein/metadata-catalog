buildscript {
  ext {
    springBootVersion = '1.5.3.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'groovy'
apply plugin: 'idea'

apply from: "${rootDir}/gradle/publishing.gradle"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

ext {
  springCloudVersion = 'Dalston.RELEASE'
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.4.10'
  compile('org.springframework.cloud:spring-cloud-starter-eureka')
  compile('org.springframework.cloud:spring-cloud-starter')
  compile('org.springframework.cloud:spring-cloud-starter-zuul')
  compile('org.springframework.boot:spring-boot-starter-web')

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4-rc-4'
  testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-4'
  testCompile 'io.rest-assured:rest-assured:3.0.2'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

configurations.configure {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
  integrationTest {
    groovy.srcDir file('src/integrationTest/groovy')
    //resources.srcDir file('src/integrationTest/resources')
    compileClasspath += main.output + test.output + configurations.testRuntime
    runtimeClasspath += output + compileClasspath
  }
}

task copyCql(type: Copy, description: 'Copies cql to build for use in docker-compose wait.', group: 'docker') {
  from '../storage/src/resources/cql'
  into "$buildDir/cql"
}

task copyCqlScript(type: Copy, description: 'Copies cql init script to build for use in docker-compose wait.', group: 'docker') {
  from '../storage/src/resources/docker/scripts'
  into "$buildDir/scripts"
  rename { fileName ->
    "${(fileName - 'template_')}" // strips the first instance of 'template_' from the filename using subtraction
  }

  expand(
      cassandra_port: "$cassandra_port",
      cassandra_service_name: "$cassandra_service_name",
      cassandra_initialization_service_name: "$cassandra_initialization_service_name",
  )
}

task buildDockerTemplates(
    type: Copy,
    description: 'Copies docker files and scripts, substituting properties for consistency.',
    group: 'docker'
) {
  from 'src/resources/docker'
  into "$buildDir"
  rename { fileName ->
    "${(fileName - 'template_')}" // strips the first instance of 'template_' from the filename using subtraction
  }

  expand(network: "$network",
      cassandra_port: "$cassandra_port",
      cassandra_service_name: "$cassandra_service_name",
      cassandra_initialization_service_name: "$cassandra_initialization_service_name",
      storage_service_name: "$storage_service_name",
      storage_port: "$storage_port",
      rabbit_service_name: "$rabbit_service_name",
      rabbit_port: "$rabbit_port",
      rabbit_admin_port: "$rabbit_admin_port",
      elasticsearch_service_name: "$elasticsearch_service_name",
      elasticsearch_rest_port: "$elasticsearch_rest_port",
      elasticsearch_node_port: "$elasticsearch_node_port",
      index_port: "$index_port",
      index_service_name: "$index_service_name",
      index_image: "ciresncei/${rootProject.name}-${rootProject.childProjects.index.name}:${project.version}",
      storage_image: "ciresncei/${rootProject.name}-${rootProject.childProjects.storage.name}:${project.version}",
      storage_jar: "${rootProject.childProjects.storage.name}-${project.version}.jar",
      index_jar: "${rootProject.childProjects.index.name}-${project.version}.jar",
      jar_dir: 'libs',
      jar: "${project.name}-${project.version}.jar")
}

static def getDateTime() {
  return new Date().format("YYYY-MM-DD'T'HH:mm:ss.SSS'Z'", TimeZone.getTimeZone('UTC'))
}

task dockerImage(type: Exec, description: 'Creates a docker image with the current jar.', group: 'docker') {
  dependsOn buildDockerTemplates
  dependsOn bootRepackage
  executable "bash"
  args "-c", "docker build --no-cache \
    --label ncei.${rootProject.name}-version=${project.version} \
    --label ncei.${rootProject.name}-build-date=" + getDateTime() + " \
    --label ncei.${rootProject.name}-name=${project.name}Module \
    --label ncei.${rootProject.name}-vendor=NCEI \
    -t ciresncei/${rootProject.name}-${project.name}:${project.version} ${buildDir}"
}

assemble.dependsOn dockerImage

task dockerComposeUp(type: Exec, description: 'Starts the system using docker-compose.', group: 'docker') {
  dependsOn ':storage:dockerImage', ':index:dockerImage', copyCql, copyCqlScript, buildDockerTemplates
  executable "bash"
  args "-c", "docker-compose -f $buildDir/docker-compose.yml -p ${project.name} up -d"
}

task dockerComposeDown(type: Exec, description: 'Stops the system using docker-compose.', group: 'docker') {
  executable "bash"
  args "-c", "docker-compose -f $buildDir/docker-compose.yml -p ${project.name} down || true"
}

task waitForStorage(
    type: Exec,
    description: 'Waits for storage to be available.',
    group: 'docker',
) {
  dependsOn dockerComposeUp
  executable "bash"
  args "-c", "docker run \
    --network ${project.name}_$network \
    --rm willwill/wait-for-it \
    -h storage \
    -p 8080 -t 90"
}

task waitForIndex(
    type: Exec,
    description: 'Waits for index to be available.',
    group: 'docker',
) {
  dependsOn dockerComposeUp
  executable "bash"
  args "-c", "docker run \
    --network ${project.name}_$network \
    --rm willwill/wait-for-it \
    -h index \
    -p 8080 -t 90"
}

task integrationTest(
    type: Test,
    description: 'Runs the integration tests',
    group: 'verification',
) {
  mustRunAfter test
  dependsOn waitForStorage
  dependsOn waitForIndex
  finalizedBy dockerComposeDown

  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
  reports.junitXml.destination = 'build/integration-test-results'
  reports.html.destination = 'build/reports/integrationTests'
}

check.dependsOn integrationTest

ext {
  bintrayUser = System.getenv('DOCKER_USER')
  bintrayKey = System.getenv('DOCKER_PASSWORD')
}

task publishImage(type: Exec, description: 'Publishes the docker image', group: 'publish') {
  dependsOn "dockerImage"
  executable "bash"
  args "-c", "docker login -u \$DOCKER_USER -p \$DOCKER_PASSWORD && docker push ciresncei/${rootProject.name}-${project.name}:${project.version} && docker logout"
}

publishing {
  publications {
    main(MavenPublication) {
      artifact jar

      groupId 'edu.colorado.cires.metadata-catalog'
      artifactId "${rootProject.name}-${project.name}"
      version "${project.version}"
    }
  }
}
